grammar org.nanosite.remix.Remix with org.eclipse.xtext.common.Terminals

generate remix "http://www.nanosite.org/remix/Remix"

Model:
	imports+=Import*
	themes+=Theme*
	collections+=Collection*
	presentations+=Presentation*;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

QualifiedNameWithWildcard:
	QualifiedName '.*'?
	;

Theme:
	'theme' name=ID (description=STRING)?;

Collection:
	'collection' name=QualifiedName ('=' path=STRING)?
		modules+=Module*
	;

Module:
	'module' name=QualifiedName '=' file=STRING
		dependencies+=Dependency*
	;

Dependency:
	"needs" type=DependencyType file=STRING 
	;

enum DependencyType:
	css="css";

Presentation:
	'presentation' name=ID title=STRING
		('description' description=STRING)?
		'author' author=STRING
		('duration' duration=INT 'minutes')?
		('theme' theme=[Theme|ID])?
		('modes' modes+=Mode (',' modes+=Mode)*)?
		('config' config=Configuration)?
		('partattr' partattr=STRING)?
		parts+=Part*
		('=>' target=STRING)?
	;

Mode:
	name=ID;

Configuration:
	active=[Mode|ID];

Part:
	{Part}
	(
		'part'
			(
				title=STRING
				(image=STRING)?
			)?
			specials+=TodoPage*
			modules+=ModuleRef*
	) |
	(
		overview?='overview'
	)
	;

TodoPage:
	'todo' todos+=STRING (',' todos+=STRING)*;

ModuleRef:
	module=[Module|QualifiedName]
	('when' selector=[Mode|ID])?
	;

QualifiedName:
	ID ('.' ID)*;
